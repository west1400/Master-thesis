CLEAR REVERT=NO
$string yesorno YES 

// parameters
VARIABLE OPTION=PARAMETER, NAME=#CoilCurrent, VALUE=325, DESCRIPTION='current in each coil'

// Make top coil (centre of the top plane of the top coil is at (0,0,0)
// Four straights
STRAIGHT WIDTH=14.37 THICKNESS=8.41 H1=36.2 XCEN2=23.065 YCEN2=-4.205 ZCEN2=-18.1 THETA2=0 PHI2=0 PSI2=0 CURD=#CoilCurrent/AREA 

STRAIGHT WIDTH=14.37 THICKNESS=8.41 H1=21.59 XCEN2=10.795 YCEN2=-4.205 ZCEN2=30.365 THETA2=90 PHI2=180 PSI2=-180 CURD=#CoilCurrent/AREA

STRAIGHT WIDTH=14.37 THICKNESS=8.41 H1=36.2 XCEN2=-23.065 YCEN2=-4.205 ZCEN2=18.1 THETA2=180 PHI2=0 PSI2=0 CURD=#CoilCurrent/AREA 

STRAIGHT WIDTH=14.37 THICKNESS=8.41 H1=21.59 XCEN2=-10.795 YCEN2=-4.205 ZCEN2=-30.365 THETA2=90 PHI2=0 PSI2=0 CURD=#CoilCurrent/AREA 

// Four arcs 

ARC WIDTH=8.41 THICKNESS=14.37 R1=5.08 ANGLE=90 XCEN2=23.065 YCEN2=-4.205 ZCEN2=18.1 THETA2=0 PHI2=0 PSI2=90 CURD=#CoilCurrent/AREA 
ARC WIDTH=8.41 THICKNESS=14.37 R1=5.08 ANGLE=90 XCEN2=-10.795 YCEN2=-4.205 ZCEN2=30.365 THETA2=90 PHI2=180 PSI2=-90 CURD=#CoilCurrent/AREA 
ARC WIDTH=8.41 THICKNESS=14.37 R1=5.08 ANGLE=90 XCEN2=-23.065 YCEN2=-4.205 ZCEN2=-18.1 THETA2=180 PHI2=0 PSI2=90 CURD=#CoilCurrent/AREA 
ARC WIDTH=8.41 THICKNESS=14.37 R1=5.08 ANGLE=90 XCEN2=10.795 YCEN2=-4.205 ZCEN2=-30.365 THETA2=90 PHI2=0 PSI2=90 CURD=#CoilCurrent/AREA 

PICK OPTION=ADD PROPERTY=Coiltype TYPE=ARC
PICK OPTION=ADD PROPERTY=Coiltype TYPE=STRAIGHT
TRANSFORM OPTION=COPY TYPE=DISPLACE DU=0 DV=-9.042 DW=0 COUNT=3 KEEP=NO

// Make lower yoke section
// Two blocks - the main bulk and raised section
BLOCK NAME='L1' X0=-52.07 Y0=-71.801 Z0=37.465 X1=62.23 Y1=-2.586 Z1=-37.465 MATERIALLABEL='ASI1010'
BLOCK NAME='L2' X0=52.07 Y0=-2.586 Z0=-37.465 X1=62.23 Y1=16.464 Z1=0.475 MATERIALLABEL='ASI1010'
// Two cuts - the hollowed section of the yoke and the angled side cut
BLOCK NAME='C1' X0=-33.02 Y0=-52.751 Z0=37.465 X1=33.02 Y1=-2.586 Z1=-37.465
BLOCK NAME='C2' X0=41.164 Y0=-71.801 Z0=37.465 X1=79.97 Y1=16.464 Z1=-50.465

// Perform cuts and merge components
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='L1'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='C1'
COMBINE OPERATION=SUBTRACT +REGULAR

PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='L1'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='L2'
COMBINE OPERATION=UNION +REGULAR

PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='C2'
TRANSFORM OPTION=APPLY TYPE=DISPLACE DU=-41.164 DV=71.801 DW=-37.465 KEEP=YES
TRANSFORM OPTION=APPLY TYPE=ROTATE ROTU=0 ROTV=1 ROTW=0 ANGLE=-15 KEEP=YES
TRANSFORM OPTION=APPLY TYPE=DISPLACE DU=41.164 DV=-71.801 DW=37.465

PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='L1'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='C2'
COMBINE OPERATION=SUBTRACT +REGULAR

// Make upper yoke section
// Two blocks - top plate and yoke within coils
BLOCK NAME='U1' X0=-52.07 Y0=0.635 Z0=37.465 X1=52.07 Y1=19.685 Z1=-37.465 MATERIALLABEL='AISI1010'
BLOCK NAME='U2' X0=-15.24 Y0=0.635 Z0=-22.542 X1=15.24 Y1=-39.37 Z1=22.542 MATERIALLABEL='AISI1010'
// One cut - angled section
BLOCK NAME='C3' X0=41.164 Y0=-71.801 Z0=37.465 X1=79.97 Y1=20.464 Z1=-50.465

// Perform cuts and merge components
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='C3'
TRANSFORM OPTION=APPLY TYPE=DISPLACE DU=-41.164 DV=71.801 DW=-37.465 KEEP=YES
TRANSFORM OPTION=APPLY TYPE=ROTATE ROTU=0 ROTV=1 ROTW=0 ANGLE=-15 KEEP=YES
TRANSFORM OPTION=APPLY TYPE=DISPLACE DU=41.164 DV=-71.801 DW=37.465

PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='U1'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='C3'
COMBINE OPERATION=SUBTRACT +REGULAR

PICK OPTION=TOGGLE TYPE=EDGE UNIQUEBODYNAME='U2' IDENTIFIER=A.00001
PICK OPTION=TOGGLE TYPE=EDGE UNIQUEBODYNAME='U2' IDENTIFIER=A.00003
PICK OPTION=TOGGLE TYPE=EDGE UNIQUEBODYNAME='U2' IDENTIFIER=A.00005
PICK OPTION=TOGGLE TYPE=EDGE UNIQUEBODYNAME='U2' IDENTIFIER=A.00007
BLEND OPTION=BLEND RADIUS=4.434

PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='U1'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='U2'
COMBINE OPERATION=UNION +REGULAR

// Assign properties to yoke material
// TODO: ADD B-H CURVE INSTEAD OF LINEAR MODEL
MATERIALS OPTION=PICK MATERIALLABEL='AISI1010'
MATERIALS OPTION=MODIFY MULINEARITY=LINEAR MUANISOTROPY=ISOTROPIC MU=902.6 HC=-318.31

FILTER TYPE=CELL
PICK OPTION=TOGGLE TYPE=CELL UNIQUEBODYNAME='L1' IDENTIFIER=A.00001
PICK OPTION=TOGGLE TYPE=CELL UNIQUEBODYNAME='U1' IDENTIFIER=A.00001
CELLDATA OPTION=MODIFY MATERIALLABEL='AISI1010' POTENTIAL=Total ELEMENTTYPE=Linear LEVEL=5 ELEMSHAPEPREF=NONE

// Add plates
// Total 10 plates of a different material
// First 7 are all in contact and form the section through which the beamlines pass
BLOCK NAME='P1' X0=15.24 Y0=19.685 Z0=37.465 X1=-15.24 Y1=0.711 Z1=41.351 MATERIALLABEL='AISI1006' 
BLOCK NAME='P2' X0=15.24 Y0=-45.842 Z0=37.465 X1=-15.24 Y1=-71.801 Z1=41.351 MATERIALLABEL='AISI1006' 
BLOCK NAME='P3' X0=15.24 Y0=-71.801 Z0=41.351 X1=-15.24 Y1=19.685 Z1=45.085 MATERIALLABEL='AISI1006' 
BLOCK NAME='P4' X0=15.24 Y0=-52.751 Z0=37.465 X1=-15.24 Y1=-45.842 Z1=-37.465 MATERIALLABEL='AISI1006' 
BLOCK NAME='P5' X0=15.24 Y0=19.685 Z0=-41.351 X1=-15.24 Y1=0.711 Z1=-37.465 MATERIALLABEL='AISI1006' 
BLOCK NAME='P6' X0=15.24 Y0=-45.842 Z0=-41.351 X1=-15.24 Y1=-71.801 Z1=-37.465 MATERIALLABEL='AISI1006' 
BLOCK NAME='P7' X0=15.24 Y0=-71.801 Z0=-41.351 X1=-15.24 Y1=19.685 Z1=-45.085 MATERIALLABEL='AISI1006' 

PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='P1'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='P2'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='P3'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='P4'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='P5'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='P6'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='P7'
COMBINE OPERATION=UNION +REGULAR

// Remaining 3 plates space the blocks
BLOCK NAME='P8' X0=-52.07 Y0=-2.586 Z0=37.465 X1=-33.02 Y1=0.635 Z1=-37.465 MATERIALLABEL='AISI1006' 

BLOCK NAME='P9' X0=52.07 Y0=19.685 Z0=-37.465 X1=62.23 Y1=16.464 Z1=0.475 MATERIALLABEL='AISI1006' 
BLOCK NAME='C4' X0=41.164 Y0=-71.801 Z0=37.465 X1=79.97 Y1=20 Z1=-50.465
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='C4'
TRANSFORM OPTION=APPLY TYPE=DISPLACE DU=-41.164 DV=71.801 DW=-37.465 KEEP=YES
TRANSFORM OPTION=APPLY TYPE=ROTATE ROTU=0 ROTV=1 ROTW=0 ANGLE=-15 KEEP=YES
TRANSFORM OPTION=APPLY TYPE=DISPLACE DU=41.164 DV=-71.801 DW=37.465
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='P9'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='C4'
COMBINE OPERATION=SUBTRACT +REGULAR

BLOCK NAME='P10' X0=33.02 Y0=-2.586 Z0=-37.465 X1=52.07 Y1=0.635 Z1=37.465 MATERIALLABEL='AISI1006' 
BLOCK NAME='C5' X0=41.164 Y0=-71.801 Z0=37.465 X1=79.97 Y1=20 Z1=-50.465
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='C5'
TRANSFORM OPTION=APPLY TYPE=DISPLACE DU=-41.164 DV=71.801 DW=-37.465 KEEP=YES
TRANSFORM OPTION=APPLY TYPE=ROTATE ROTU=0 ROTV=1 ROTW=0 ANGLE=-15 KEEP=YES
TRANSFORM OPTION=APPLY TYPE=DISPLACE DU=41.164 DV=-71.801 DW=37.465
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='P10'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='C5'
COMBINE OPERATION=SUBTRACT +REGULAR

// Cut holes through plate
// Two holes for the BL1U beam pipe
CYLINDER NAME='H1' X0=0 Y0=0 Z0=-4 X1=0 Y1=0 Z1=1 MAJORRADIUS=2.3815 MINORRADIUS=2.3815 TOPRADIUS=2.3815 TUBE=NO

PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='H1'
TRANSFORM OPTION=APPLY TYPE=ROTATE ROTU=0 ROTV=1 ROTW=0 ANGLE=4.5 KEEP=YES

TRANSFORM OPTION=APPLY TYPE=DISPLACE DU=5.183532902 DV=-43.132 DW=45.085

PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='P1'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='H1'
COMBINE OPERATION=SUBTRACT +REGULAR

CYLINDER NAME='H2' X0=0 Y0=0 Z0=4 X1=0 Y1=0 Z1=-1 MAJORRADIUS=2.3815 MINORRADIUS=2.3815 TOPRADIUS=2.3815 TUBE=NO
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='H2'
TRANSFORM OPTION=APPLY TYPE=ROTATE ROTU=0 ROTV=1 ROTW=0 ANGLE=4.5 KEEP=YES
TRANSFORM OPTION=APPLY TYPE=DISPLACE DU=-1.913 DV=-43.132 DW=-45.085
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='P1'
PICK OPTION=TOGGLE PROPERTY=UniqueName LABEL='H2'
COMBINE OPERATION=SUBTRACT +REGULAR

// Assign properties to plates
// TODO: ADD B-H CURVE
MATERIALS OPTION=PICK MATERIALLABEL='AISI1006'
MATERIALS OPTION=MODIFY MULINEARITY=LINEAR MUANISOTROPY=ISOTROPIC MU=1404 HC=-119.36

FILTER TYPE=CELL
PICK OPTION=TOGGLE TYPE=CELL UNIQUEBODYNAME='P1' IDENTIFIER=A.00001
PICK OPTION=TOGGLE TYPE=CELL UNIQUEBODYNAME='P8' IDENTIFIER=A.00001
PICK OPTION=TOGGLE TYPE=CELL UNIQUEBODYNAME='P9' IDENTIFIER=A.00001
PICK OPTION=TOGGLE TYPE=CELL UNIQUEBODYNAME='P10' IDENTIFIER=A.00001
CELLDATA OPTION=MODIFY MATERIALLABEL='AISI1006' POTENTIAL=Total ELEMENTTYPE=Linear LEVEL=4 ELEMSHAPEPREF=NONE

// Create reduced potential region around coils
BLOCK NAME='R1' X0=33.02 Y0=0.635 Z0=-41.351 X1=-33.02 Y1=-38 Z1=41.351 MATERIALLABEL='Air'
FILTER TYPE=CELL
PICK OPTION=TOGGLE TYPE=CELL UNIQUEBODYNAME='R1' IDENTIFIER=A.00001
CELLDATA OPTION=MODIFY MATERIALLABEL='Air' POTENTIAL=Reduced ELEMENTTYPE=Linear LEVEL=3 ELEMSHAPEPREF=NONE

// Create fine region in roi
BLOCK NAME='R2' X0=33.02 Y0=-38 Z0=-41.351 X1=-33.02 Y1=-52.751 Z1=41.351 MATERIALLABEL='Air'
FILTER TYPE=CELL
PICK OPTION=TOGGLE TYPE=CELL UNIQUEBODYNAME='R2' IDENTIFIER=A.00001
CELLDATA OPTION=MODIFY MATERIALLABEL='Air' POTENTIAL=Reduced ELEMENTTYPE=Linear LEVEL=2 ELEMSHAPEPREF=NONE

// create background
BACKGROUND OPTION=SET SHAPE=BLOCK SCALEX=1.5 SCALEY=1.5 SCALEZ=1.5
$abortcomi
// Make model body
MODEL CREATE

//Set analysis type
ANALYSISDATA OPTION=SET PROGRAM=TOSCAMAGN LINEAR=NO NLITERTYPE=NEWTON NITERATIONS=21 TOLERANCE=1.0E-03 HX=0 HY=0 HZ=0 RHS=ADAPTIVE POTENTIALCUT=YES 

// Create surface mesh
MESH GENERATOR=AUTOMATIC SIZE=5 NORMALTOL=30.0 SURFACETOL=0.0 TOLERANCE=1.0E-06 TYPE=PREFERTETRA 
// Create volume mesh
FILL TOL=1.0E-06 
$abortcomi
// Create the Analysis Database
SOLVERS SOLVENOW=YES SAVEMODEL=YES, | SOLVERS OPTION=TEST FILE='C:\Users\sweitz\Desktop\SeptumDatabase.op3' UNITS=MM ELEMENT=MIXED SURFACE=CURVED | COMMENT CLEAR=YES TYPE=DBTITLE | SOLVERS OPTION=OVERWRITE

//Finished
// These two commands can be entered in the postprocessor!
//CARTESIAN CORNERS=4, X1=-60 Y1=-60 Z1=0 X2=-60 Y2=20 Z2=0 X3=60 Y3=20 Z3=0 X4=60 Y4=-60 Z4=0 N1=500 N2=500 BUFFER=Cartesian | MAP FILE=TEMP COMPONENT=B
//TABLE INFILE=TEMP  OUTFILE='..\Septum-Opera\septum.tab' COLUMNS=7 F1=X UNIT1T=LENGU F2=Y UNIT2T=LENGU F3=Z UNIT3T=LENGU F4=B UNIT4T=FLUXU F5=Bx UNIT5T=FLUXU F6=By UNIT6T=FLUXU F7=Bz UNIT7T=FLUXU 
//GRID X0=-400 Y0=-700 Z0=-500 DXG=10 DYG=10 DZG=10 NXG=80 NYG=100 NZG=90 FILE='..\Septum-Opera\septum.table' BINARY=NO FORMAT=2 F1=X UNIT1G=LENGU F2=Y UNIT2G=LENGU F3=Z UNIT3G=LENGU F4=Bx UNIT4G=FLUXU F5=By UNIT5G=FLUXU F6=Bz UNIT6G=FLUXU UNIT7G=1 UNIT8G=1 UNIT9G=1 UNIT10G=1 UNIT11G=1 UNIT12G=1